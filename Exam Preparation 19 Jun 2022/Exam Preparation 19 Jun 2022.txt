01. Database Design


CREATE DATABASE Zoo
GO

USE Zoo
GO

CREATE TABLE Owners(
Id INT PRIMARY KEY IDENTITY,
[Name] VARCHAR(50) NOT NULL,
PhoneNumber VARCHAR(15) NOT NULL,
[Address] VARCHAR(50)
)

CREATE TABLE AnimalTypes(
Id INT PRIMARY KEY IDENTITY,
AnimalType VARCHAR(30) NOT NULL
)

CREATE TABLE Cages(
Id INT PRIMARY KEY IDENTITY,
AnimalTypeId INT FOREIGN KEY REFERENCES AnimalTypes(Id) NOT NULL
)

CREATE TABLE Animals(
Id INT PRIMARY KEY IDENTITY,
[Name] VARCHAR(30) NOT NULL,
BirthDate DATE NOT NULL,
OwnerId INT FOREIGN KEY REFERENCES Owners(Id),
AnimalTypeId INT FOREIGN KEY REFERENCES AnimalTypes(Id) NOT NULL
)

CREATE TABLE AnimalsCages(
CageId INT FOREIGN KEY REFERENCES Cages(Id) NOT NULL,
AnimalId INT FOREIGN KEY REFERENCES Animals(Id) NOT NULL,
CONSTRAINT PK_AnimalsCages PRIMARY KEY (CageId,AnimalId)
)

CREATE TABLE VolunteersDepartments(
Id INT PRIMARY KEY IDENTITY,
DepartmentName VARCHAR(30) NOT NULL
)

CREATE TABLE Volunteers(
Id INT PRIMARY KEY IDENTITY,
[Name] VARCHAR (50) NOT NULL,
PhoneNumber VARCHAR (15) NOT NULL,
[Address] VARCHAR (50),
AnimalId INT FOREIGN KEY REFERENCES Animals(Id),
DepartmentId INT FOREIGN KEY REFERENCES VolunteersDepartments(Id) NOT NULL
)
------------------------------------------------------------------------------------------------

02. Insert

INSERT INTO Volunteers([Name], PhoneNumber, [Address], AnimalId, DepartmentId)
VALUES
('Anita Kostova',	'0896365412',	'Sofia, 5 Rosa str.',	15,	1),
('Dimitur Stoev',	'0877564223',	null,	42,	4),
('Kalina Evtimova',     '0896321112',	'Silistra, 21 Breza str.',	9,	7),
('Stoyan Tomov',	'0898564100',	'Montana, 1 Bor str.',	18,	8),
('Boryana Mileva',	'0888112233',	null,	31,	5);

INSERT INTO Animals ([Name], BirthDate, OwnerId, AnimalTypeId)
VALUES
('Giraffe', 	        '2018-09-21',	21,	1),
('Harpy Eagle',	        '2015-04-17',	15,	3),
('Hamadryas Baboon',	'2017-11-02',	null,	1),
('Tuatara',		'2021-06-30',	2,	4);
-----------------------------------------------------------------------------------------------

03. Update

UPDATE Animals
SET OwnerId = 4
WHERE OwnerId IS NULL
------------------------------------------------------------------------------------------------

04. Delete

--SELECT * FROM VolunteersDepartments 
--WHERE DepartmentName = 'Education program assistant' -- =>2

--SELECT * FROM Volunteers
--WHERE DepartmentId =2  --=>  (1, 7, 15, 18, 19, 22)

DELETE 
FROM Volunteers
WHERE Id IN (1, 7, 15, 18, 19, 22)

DELETE 
FROM VolunteersDepartments
WHERE Id =2;
-------------------------------------------------------------------------------------------------

05. Volunteers

  SELECT 
         [Name], PhoneNumber, [Address], AnimalId, DepartmentId
    FROM Volunteers
ORDER BY [Name], AnimalId, DepartmentId
-----------------------------------------------------------------------------------------------

06. Animals data

SELECT 
	a.[Name],
	t.AnimalType,
	FORMAT(a.BirthDate, 'dd.MM.yyyy') AS BirthDate
FROM Animals AS a
JOIN AnimalTypes AS t ON a.AnimalTypeId = t.Id
ORDER BY a.[Name]
-------------------------------------------------------------------------------------------------

07. Owners and Their Animals

  SELECT TOP 5
         o.[Name],
	 COUNT(a.Id) AS CountOfAnimals
    FROM Owners AS o
    JOIN Animals AS a ON o.Id = a.OwnerId
GROUP BY o.[Name]
ORDER BY COUNT(a.Id) DESC,
	 o.[Name]
-------------------------------------------------------------------------------------------------

08. Owners, Animals and Cages

  SELECT 
         CONCAT(o.[Name], '-', a.[Name]) AS OwnersAnimals,
         o.PhoneNumber,
         c.CageId AS CageId
    FROM Animals AS a
    JOIN Owners AS o ON o.Id = a.OwnerId
    JOIN AnimalTypes AS t ON a.AnimalTypeId = t.Id 
    JOIN AnimalsCages AS c ON a.Id = c.AnimalId
   WHERE t.Id = 1 
ORDER BY o.[Name], 
         a.[Name] DESC
-------------------------------------------------------------------------------------------------

09. Volunteers in Sofia

  SELECT 
         v.[Name],
         v.PhoneNumber,
         TRIM(REPLACE(REPLACE(v.[Address], 'Sofia', ''), ',', '')) AS [Address]
    FROM Volunteers AS v
    JOIN VolunteersDepartments AS d ON v.DepartmentId = d.Id
   WHERE d.DepartmentName = 'Education program assistant' 
     AND v.[Address] LIKE '%Sofia%'
ORDER BY v.[Name]
-----------------------------------------------------------------------------------------------

10. Animals for Adoption

   SELECT
          a.[Name],
          DATEPART(YEAR, a.BirthDate) AS BirthYear,
          t.AnimalType
     FROM Animals AS a
     JOIN AnimalTypes AS t ON a.AnimalTypeId = t.Id
    WHERE a.OwnerId IS NULL
      AND (DATEDIFF(YEAR, a.BirthDate, '01/01/2022' ))<5
      AND t.AnimalType <> 'Birds'
ORDER BY a.[Name]
-----------------------------------------------------------------------------------------------

11. All Volunteers in a Department

CREATE FUNCTION udf_GetVolunteersCountFromADepartment (@VolunteersDepartment VARCHAR(30))
	RETURNS INT
	AS
	BEGIN
		DECLARE @result INT
			SELECT @result = COUNT(*) 
			FROM VolunteersDepartments AS d
			JOIN Volunteers AS v ON d.Id = v.DepartmentId
			WHERE d.DepartmentName = @VolunteersDepartment
		RETURN @result
	END
-------------------------------------------------------------------------------------------------

12. Animals with Owner or Not

CREATE PROCEDURE usp_AnimalsWithOwnersOrNot(@AnimalName VARCHAR (30))
	AS
		BEGIN
		    SELECT 
			  a.[Name],
		          --ISNULL(o.[Name], 'For adoption')
			  CASE 
				WHEN o.[Name] IS NULL THEN 'For adoption'
				ELSE o.[Name]
			  END 
			  AS OwnerName
		     FROM Animals AS a
		LEFT JOIN Owners AS o ON a.OwnerId = o.Id
	            WHERE a.[Name] = @AnimalName
	    END






